plugins {
    id 'java'
    id "io.qameta.allure" version "2.8.1"
}

apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

group 'bdd.tests'
version '1.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.1'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.28'
    testImplementation group: 'io.cucumber', name: 'cucumber-java', version: '2.4.0'
    testImplementation group: 'io.cucumber', name: 'cucumber-picocontainer', version: '2.4.0'
    testImplementation group: 'io.cucumber', name: 'cucumber-testng', version: '2.4.0'
    testImplementation group: 'io.qameta.allure', name: 'allure-cucumber2-jvm', version: '2.13.0'
}

allure {
    version = '2.13.3'
    resultsDir = file("${rootProject.rootDir}/build/allure-results")
    reportDir = file("${rootProject.rootDir}/build/allure-report")
}

checkstyle {
    toolVersion = '8.23'
    configFile = file("${rootProject.rootDir}/config/checkstyle/checkstyle.xml")
}

pmd {
    toolVersion = '6.0.0'
}

tasks.withType(Pmd) {
    ruleSets = ["java-basic", "java-braces", "java-unnecessary", "java-unusedcode", "java-strictexception"]
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task executeBDDTests(type: Test) {
    doFirst {
        useTestNG()
        options {
            useDefaultListeners = false
            jvmArgs = ['-Dcucumber.options= --plugin pretty --plugin io.qameta.allure.cucumber2jvm.AllureCucumber2Jvm '
                               + cucumberOptions]
        }
        testLogging.showStandardStreams = true
        scanForTestClasses = false
    }
    finalizedBy 'allureReport'
}
